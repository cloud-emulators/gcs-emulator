plugins {
    id 'java'
    id "com.github.psxpaul.execfork" version "0.2.2"
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
}

ext {
    slf4jVersion = '2.0.7'
    jettyVersion = '11.0.15'
    jerseyVersion = '3.1.1'
    restEasyVersion = '6.2.3.Final'
}

group = 'io.github.cloud-emulators'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    implementation(platform("com.google.cloud:libraries-bom:26.13.0"))
    implementation("com.google.cloud:google-cloud-storage")
    implementation("com.google.cloud:google-cloud-nio")

    implementation "org.slf4j:slf4j-api:${slf4jVersion}"
    implementation "org.slf4j:slf4j-simple:${slf4jVersion}"


    implementation("org.jboss.resteasy:resteasy-undertow:${restEasyVersion}")
    implementation("org.jboss.resteasy:resteasy-multipart-provider:${restEasyVersion}")
    implementation("org.reflections:reflections:0.10.2")

    implementation("commons-io:commons-io:2.11.0")

    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation("junit:junit:4.13.2")
    testImplementation("org.awaitility:awaitility:4.2.0")


}

test {
//    useJUnitPlatform()

    maxParallelForks 1
}

tasks.register('startServerInternal', com.github.psxpaul.task.JavaExecFork) {
    dependsOn(jar)
    dependsOn(compileTestJava)
    group = "LifeCycle"
    description = "Start the servers"
    classpath = sourceSets.main.runtimeClasspath
    main = 'io.github.cloudemulators.gcsemulator.lifecycle.Starter'
    waitForPort = 8080
}

tasks.register('startServer', JavaExec) {
    group = "LifeCycle"
    description = "Start the servers"
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'io.github.cloudemulators.gcsemulator.lifecycle.Starter'
}

tasks.register('checkServer', JavaExec) {
    group = "LifeCycle"
    description = "Check if the servers is running blocking till it does"
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'io.github.cloudemulators.gcsemulator.lifecycle.Checker'
}

tasks.register('stopServer', JavaExec) {
    group = "LifeCycle"
    description = "Stop the servers"
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'io.github.cloudemulators.gcsemulator.lifecycle.Stopper'
}

test.dependsOn(startServerInternal)